plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.ciprianmosincat'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation("com.querydsl:querydsl-jpa:${querydslVersion}")
    implementation("com.querydsl:querydsl-apt:${querydslVersion}")

    runtimeOnly 'mysql:mysql-connector-java'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor(
            'org.springframework.boot:spring-boot-configuration-processor',
            "com.querydsl:querydsl-apt:${querydslVersion}:jpa",
            'org.projectlombok:lombok',
            'org.springframework.boot:spring-boot-starter-data-jpa',
    )

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:mysql:${testContainersMysqlVersion}"
    testImplementation 'mysql:mysql-connector-java'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'

}

test {
    useJUnitPlatform()
}

configurations {
    itestCompile.extendsFrom testCompile
    itestImplementation.extendsFrom testImplementation
}

sourceSets {
    itest {
        compileClasspath += main.output
        runtimeClasspath += main.output
        compileClasspath += test.output
        runtimeClasspath += test.output

        java.srcDir file('src/itests/java')
        resources.srcDir file('src/itest/resources')
    }
}

task itest(type: Test) {
    testClassesDirs = sourceSets.itest.output.getClassesDirs()
    classpath = sourceSets.itest.runtimeClasspath
}
